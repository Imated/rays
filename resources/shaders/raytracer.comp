#version 460 core

layout(local_size_x=8, local_size_y=8) in;

layout(rgba16f, binding = 0) uniform image2D accumImage;

struct Material {
    vec3 color;
    float smoothness;
    vec3 emissiveColor;
    float emissiveStrength;
};

struct Ray {
    vec3 origin;
    vec3 direction;
};

struct HitInfo {
    bool didHit;
    float distance;
    vec3 hitPos;
    vec3 normal;
    Material material;
};

struct MeshInfo {
    uint firstTriangleIndex;
    uint numTriangles;
    vec4 boundingBoxMin;
    vec4 boundingBoxMax;
    vec4 color_smoothness;
    vec4 emissionColor_emissionStrength;
};
layout (std430, binding = 2) buffer MeshBuffer {
    MeshInfo meshes[];
};

struct Triangle {
    vec4 posA;
    vec4 posB;
    vec4 posC;
    vec4 normalA;
    vec4 normalB;
    vec4 normalC;
};
layout (std430, binding = 1) buffer TriangleBuffer {
    Triangle triangles[];
};

struct Sphere {
    vec4 pos_radius;
    vec4 color_smoothness;
    vec4 emissiveColor_strength;
};
layout (std430, binding = 0) buffer SphereBuffer {
    Sphere spheres[];
};

uniform uvec2 uResolution;
uniform uint renderedFrames;
uniform int samplesPerPixel;
uniform vec3 cameraPosition;
uniform mat3 cameraRotation;
uniform float uFocalLength;
uniform bool shouldAccumulate;

uint lcg(inout uint s) {
    s = 1664525u * s + 1013904223u;
    return s;
}
float rnd(inout uint s) {
    return uintBitsToFloat((lcg(s) >> 9) | 0x3f800000u) - 1.0;
}

vec3 randomDirection(inout uint rngState) {
    float z  = 1.0 - 2.0 * rnd(rngState);
    float a  = 6.28318530718 * rnd(rngState);
    float r  = sqrt(max(0.0, 1.0 - z*z));
    float ca = cos(a), sa = sin(a);
    return vec3(r*ca, r*sa, z);
}

HitInfo intersectRaySphere(Ray ray, Sphere sphere) {
    vec3 oc = sphere.pos_radius.xyz - ray.origin;
    float a = dot(ray.direction, ray.direction);
    float b = -2.0 * dot(ray.direction, oc);
    float c = dot(oc, oc) - sphere.pos_radius.w * sphere.pos_radius.w;
    float discriminant = b*b - 4.0 * a * c;

    HitInfo hitInfo;
    hitInfo.didHit = false;

    if (discriminant >= 0.0) {
        hitInfo.distance = (-b - sqrt(discriminant)) / (2.0 * a);

        if (hitInfo.distance > 0.0) {
            hitInfo.didHit = true;
            hitInfo.hitPos = ray.origin + ray.direction * hitInfo.distance;
            hitInfo.normal = normalize(hitInfo.hitPos - sphere.pos_radius.xyz);
            Material material;
            material.color = sphere.color_smoothness.xyz;
            material.emissiveColor = sphere.emissiveColor_strength.xyz;
            material.emissiveStrength = sphere.emissiveColor_strength.w;
            material.smoothness = sphere.color_smoothness.w;
            hitInfo.material = material;
        }
    }
    return hitInfo;
}

HitInfo intersectRayTriangle(Ray ray, Triangle tri) {
    vec3 edgeAB = tri.posB.xyz - tri.posA.xyz;
    vec3 edgeAC = tri.posC.xyz - tri.posA.xyz;
    vec3 normalVector = cross(edgeAB, edgeAC);
    vec3 ao = ray.origin.xyz - tri.posA.xyz;
    vec3 dao = cross(ao, ray.direction);

    float determinant = -dot(ray.direction, normalVector);
    float inverseDeterminant = 1 / determinant;

    float dst = dot(ao, normalVector) * inverseDeterminant;
    float u = dot(edgeAC, dao) * inverseDeterminant;
    float v = -dot(edgeAB, dao) * inverseDeterminant;
    float w = 1 - u - v;

    HitInfo hitInfo;
    if (dot(normalize(normalVector), ray.direction) >= 0.0) {
        hitInfo.didHit = false;
        return hitInfo;
    }
    hitInfo.didHit = determinant >= 1e-6 && dst >= 0 && u >= 0 && v >= 0 && w >= 0;
    hitInfo.hitPos = ray.origin + ray.direction * dst;
    hitInfo.normal = normalize(tri.normalA.xyz * w + tri.normalB.xyz * u + tri.normalC.xyz * v);
    hitInfo.distance = dst;
    return hitInfo;
}

bool intersectRayBoundingBox(Ray ray, vec3 boundsMin, vec3 boundsMax) {
    vec3 invDir = 1 / ray.direction;
    vec3 tMin = (boundsMin - ray.origin) * invDir;
    vec3 tMax = (boundsMax - ray.origin) * invDir;
    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);
    float tNear = max(max(t1.x, t1.y), t1.z);
    float tFar = min(min(t2.x, t2.y), t2.z);
    return tNear <= tFar;
}

HitInfo calculateRayIntersection(Ray ray) {
    HitInfo closestHit;
    closestHit.didHit = false;
    closestHit.distance = 1.0 / 0.0;

    for (int i = 0; i < spheres.length(); i++) {
        Sphere sphere = spheres[i];
        HitInfo hitInfo = intersectRaySphere(ray, sphere);
        if (hitInfo.didHit && hitInfo.distance < closestHit.distance) {
            closestHit = hitInfo;
        }
    }

    for (int meshIndex = 0; meshIndex < meshes.length(); meshIndex++) {
        MeshInfo mesh = meshes[meshIndex];
        if(!intersectRayBoundingBox(ray, mesh.boundingBoxMin.xyz, mesh.boundingBoxMax.xyz)) {
            continue;
        }
        for (int i = 0; i < mesh.numTriangles; i++) {
            uint triIndex = mesh.firstTriangleIndex + i;
            Triangle tri = triangles[triIndex];
            HitInfo hitInfo = intersectRayTriangle(ray, tri);

            if(hitInfo.didHit && hitInfo.distance < closestHit.distance) {
                closestHit = hitInfo;
                Material material;
                material.color = mesh.color_smoothness.xyz;
                material.emissiveColor = mesh.emissionColor_emissionStrength.xyz;
                material.emissiveStrength = mesh.emissionColor_emissionStrength.w;
                material.smoothness = mesh.color_smoothness.w;
                closestHit.material = material;
            }
        }
    }
    return closestHit;
}

vec3 GetEnvironmentLight(Ray ray) {
    float a = 0.5*(ray.direction.y + 1.0);
    return mix(vec3(1), vec3(0.5, 0.7, 1.0), a);
}

uniform int maxBounces;
vec3 traceRay(Ray ray, inout uint rngState) {
    vec3 inLight = vec3(0.0);
    vec3 rayColor = vec3(1.0);
    for(int i = 0; i <= maxBounces; i++) {
        HitInfo info = calculateRayIntersection(ray);
        if(info.didHit) {
            ray.origin = info.hitPos + info.normal * 1e-4;
            Material material = info.material;
            vec3 diffuseDir = normalize(info.normal + randomDirection(rngState));
            vec3 specularDir = reflect(normalize(ray.direction), info.normal);
            ray.direction = normalize(mix(diffuseDir, specularDir, clamp(material.smoothness, 0.0, 1.0)));
            vec3 emittedLight = material.emissiveColor * material.emissiveStrength;
            inLight += emittedLight * rayColor;
            rayColor *= material.color;
        }
        else
        {
            inLight += GetEnvironmentLight(ray) * rayColor;
            break;
        }
    }
    return inLight;
}

void main() {
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    if (pixelCoord.x >= int(uResolution.x) || pixelCoord.y >= int(uResolution.y))
        return;
    uint rngState = pixelCoord.x * uResolution.x + pixelCoord.y + renderedFrames * 71933u;
    vec2 pixelCenter = vec2(pixelCoord) + vec2(rnd(rngState), rnd(rngState));
    vec2 ndc = (pixelCenter - vec2(uResolution) * 0.5);
    vec3 dir = cameraRotation * normalize(vec3(ndc, uFocalLength));
    Ray ray = Ray(cameraPosition, dir);

    vec3 curr = vec3(0);
    for(int rayIndex = 0; rayIndex < samplesPerPixel; rayIndex++)
        curr += traceRay(ray, rngState);
    curr /= float(samplesPerPixel);

    vec4 prev = imageLoad(accumImage, pixelCoord);
    float alpha = (shouldAccumulate != false) ? (1.0 / float(renderedFrames + 1u)) : 1.0;
    vec3 outCol = mix(prev.rgb, curr, alpha);
    imageStore(accumImage, pixelCoord, vec4(outCol, 1.0));
}